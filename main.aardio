import win.ui;
import win.ui.simpleWindow2;
import win.ui.menu;
import win.util.tray;
import win.timer
import web.json;
import key.hotkey; 
import config;
import api;
//import console;

var frmToolWindow = win.form( exmode="toolwindow" )
/*DSG{{*/
mainForm = win.form(text="STK";left=0;top=0;right=198;bottom=300;bgcolor=16777215;border="none";exmode="none";max=false;mode="popup";parent=frmToolWindow;title=false;topmost=1)
mainForm.add(
bk={cls="bk";text="  STK";left=0;top=0;right=199;bottom=28;ah=1;align="left";bgcolor=12632256;dl=1;dr=1;dt=1;font=LOGFONT(h=-13;name='微软雅黑';weight=700);forecolor=12632256;z=1};
button={cls="button";text="Button";left=14;top=253;right=79;bottom=287;bgcolor=255;border=1;hide=1;z=3};
listview={cls="listview";left=1;top=30;right=199;bottom=301;asel=false;bgcolor=16777215;font=LOGFONT(h=-13;name='微软雅黑';weight=900);gridLines=1;msel=false;z=2}
)
/*}}*/

win.ui.simpleWindow2(mainForm) //如果 mainForm 的最小化按钮设为隐藏(false)，则最小化按钮以及可拖动边框不会出现。
mainForm.enableDpiScaling("init")

mainForm.listview.insertColumn("简称",70,,2/*_LVCFMT_CENTER*/)
mainForm.listview.insertColumn("最新价",68,,2/*_LVCFMT_CENTER*/)
mainForm.listview.insertColumn("涨跌幅",60,,2/*_LVCFMT_CENTER*/)
//第一列居中
c=mainForm.listview.getColumn(, 1)
c.fmt=0x2/*_LVCFMT_CENTER*/
mainForm.listview.setColumn(c, 1)

//自绘
mainForm.listview.onnotify = function(id, code, ptr){
	if( code == 0xFFFFFFF4/*_NM_CUSTOMDRAW*/ ){
		var lvcd = mainForm.listview.getNotifyCustomDraw(code,ptr);
		if( lvcd.nmcd.dwDrawStage == 0x10001/*_CDDS_ITEMPREPAINT*/)
			return 0x20/*_CDRF_NOTIFYSUBITEMDRAW*/
		elseif( lvcd.nmcd.dwDrawStage == 1/*_CDDS_PREPAINT*/ ){
			return 0x20/*_CDRF_NOTIFYITEMDRAW*/;
		}
		else{ 
			//注意这里 iSubItem 的索引自0开始( 其他函数通常自1开始 )
			var itemObject = mainForm.listview.getItem(lvcd.nmcd.dwItemSpec + 1, 3)
			var textClr = gdi.RGB(255,0,0) 
			if(itemObject) {
				if(string.startWith(itemObject.text, "-", true)) 
					textClr = gdi.RGB(0,128,0)
				elseif(string.startWith(itemObject.text, "0.00%", true))
					textClr = gdi.RGB(0,0,0)
			}
			lvcd.clrText = lvcd.iSubItem > 0 ? textClr : gdi.RGB(0,0,0)
			lvcd.clrTextBk = gdi.RGB(255,255,255)
			lvcd.update()
			return 0/*_CDRF_DODEFAULT*/
		}
	}
}

var q = config.q
mainForm.rData = {}
mainForm.listview.enableDoubleBuffering()

mainForm.getData = function(mainForm, q){
	var thrdHandle = thread.create( 
		function(mainForm, q){
			import api
			mainForm.rData = api.getRData(q)
		}, mainForm, q
	)
	thread.waitOne(thrdHandle,5000)
	raw.closehandle(thrdHandle)
	var rData = mainForm.rData
	var result = {}
	if (rData) {
		var str = web.json.stringify(rData)
		var data = web.json.parse(str)
		var keys = string.split(q, ",")
		for(i=1; table.len(keys); 1){
			str = data["v_"+keys[i]]
			var strs = string.split(str, "~")
  			var map = {}
			map.name = strs[2]
			map.code = strs[3]
			map.jc = strs[4]
			map.z = strs[5]
			map.jk = strs[6]
			map.jg = strs[34]
			map.jd = strs[35]
			map.f1 = strs[32]
			map.f2 = strs[33]
			result[keys[i]] = map
		}
	}
	return result 
}

mainForm.refreshWin = function(mainForm, q){
	var result = mainForm.getData(mainForm, q)
	var items = {};
	var keys = string.split(q, ",")
	for(i=1; table.len(keys); 1){
		table.push(items, {result[keys[i]].name,result[keys[i]].jc,result[keys[i]].f2 + "%"})
	}
	//替换数据，重用旧的列表项原地更新数据，避免闪烁。
	mainForm.listview.replaceItems(items)
}

var refreshWinTimer = win.timer(mainForm, 5000);
refreshWinTimer.onTimer = function(hwnd, msg, id, tick){
	try {
    	thread.invoke(mainForm.refreshWin, mainForm, q)
    } catch(e){}
}
refreshWinTimer.enable()

mainForm.button.oncommand = function(id, event){
	mainForm.refreshWin(mainForm, q)
}

mainForm.wndproc = function(hwnd, message, wparam, lparam){
    select(message) {//判断消息类型
        case  0x10/*_WM_CLOSE*/{
            fadeHide()
        }
        case( 0x400+9981/*_WM_TRAYMESSAGE*/ ) { //托盘图标消息 
            if( lparam = 0x205/*_WM_RBUTTONUP*/ ){
                    import mouse
                    x,y = mouse.getPos()
                    
                    //弹出托盘菜单以前，一定要前置主窗口中，不然不点击菜单不会消失
                    win.setForeground(mainForm.hwnd)
                    mainForm.popmenu.popup( x,y,true )
            }
            if(lparam = 0x203/*_WM_LBUTTONDBLCLK)*/){  //鼠标双击
                mainForm.show(true)
                mainForm.popmenu.setString(1,"隐藏")
            }
        }
        case( 0x112/*_WM_SYSCOMMAND*/ ){ //系统命令消息
            if( wparam == 0xF020/*_SC_MINIMIZE*/ ){ //用户点击了最小化按钮
            	stat = win.isVisible(mainForm.hwnd)
                if(stat){
                	mainForm.show(false)
                	mainForm.popmenu.setString(1,"打开主窗口")
                }else{
                    mainForm.show(true)
                    mainForm.popmenu.setString(1,"隐藏主窗口")
                }
                mainForm.show(false) //隐藏窗口
                mainForm.popmenu.setString(1,"显示")
                return true //阻击默认消息传递，取消最小化过程
              }
        }
     }
}

fadeHide = function(){
    for(i=17; 1; -1){
        mainForm.transparent( i * 15)
        x,y,cx,cy = mainForm.getPos()
        mainForm.setPos(x-1, y-1, cx+2, cy+2)
        win.delay(5);
    }  
    win.quitMessage()
}

// 隐藏窗口
winHidden = function(){
    stat = win.isVisible(mainForm.hwnd)
    if(stat){
        mainForm.show(false)
        mainForm.popmenu.setString(1, "显示")   
    }else{
        mainForm.show(true)
        mainForm.popmenu.setString(1, "隐藏")
    }
}

//下面的代码实现用鼠标左键按住窗口时 —— 可拖动改变窗口位置
mainForm.onMouseDown  = function(wParam, lParam){
	mainForm.hitCaption()
}

mainForm.onClose = function(hwnd, message, wParam, lParam){
	var x,y = mainForm.getPos()
	config.saveQ(q)
	config.savePos(x, y)
    frmToolWindow.close()
}

mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单

mainForm.popmenu.add("隐藏", function(id){
    winHidden()
})

mainForm.isTransparent = false
mainForm.popmenu.add("窗口透明", function(id){
    if(mainForm.isTransparent){
        mainForm.transparent(false)
        mainForm.isTransparent = false
        mainForm.popmenu.setString(2, "窗口透明")   
    }else{
        mainForm.transparent(100,16777215)
        mainForm.isTransparent = true
        mainForm.popmenu.setString(2, "取消窗口透明")
    }
})

mainForm.popmenu.add() //分隔线
mainForm.popmenu.add('退出', function(id){ mainForm.close() })

tray = win.util.tray(mainForm) //创建托盘图标
tray.message = 0x400+9981/*_WM_TRAYMESSAGE*/
tray.tip = "STK小智慧" //设置鼠标提示

//批量加载热键配置表
var superHotkey = key.hotkey(mainForm) //创建超级热键,必须用于窗口程序中
setSuperHotkey = function(){
	superHotkey.loadTable({
		[config.w1] = function(hFocus){  
			winHidden()
		}
		
		[config.w2] = function(hFocus){  
			winHidden()
		}
	})
}

setSuperHotkey()
var hotkeyTimer = win.timer(mainForm, 5000);
hotkeyTimer.onTimer = function(hwnd, msg, id, tick){ setSuperHotkey() }
hotkeyTimer.enable()

win.setPos(mainForm.hwnd, config.x+17, config.y+17) // 不知道为什么，设定的位置总会偏差17个像素，难道是因为缩放？
mainForm.show()
thread.invoke(mainForm.refreshWin, mainForm, q)

return win.loopMessage()